buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    compile.exclude module: "spring-boot-starter-logging"
}

dependencies {
    //spring boot
    compile("org.springframework.boot:spring-boot-starter:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-log4j2:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")

    //statistics dependencies
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
    compile("io.micrometer:micrometer-registry-influx:$influxMicrometerVersion")

    //common
    compile("org.projectlombok:lombok:$lombokVersion")
    compile("org.apache.commons:commons-lang3:$commonsLangVersion")

    //db
    compile("org.postgresql:postgresql:$postgresqlVersion")

    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
    testCompile("com.h2database:h2:$h2Version")
    testCompile("junit:junit:$junitVersion")
}

task zipPackage(type: Zip){
    from "${buildDir}/libs"
    include '**/*'
    archiveName "${rootProject.name}-${version}.zip"
    destinationDir(file("${buildDir}/dist"))
}

task copyConfiguration(type: Copy){
    from ('src/main/config'){
        include '*'
    }
    into "${buildDir}/libs/config"
    finalizedBy zipPackage
}

task copyProperties(type: Copy){
    from ('src/main/resources'){
        include 'application.properties'
    }
    into "${buildDir}/libs/config"
    finalizedBy copyConfiguration
}

bootJar {
    from ("src/main/resources/static") {
        into 'static'
    }
    finalizedBy copyProperties
}
